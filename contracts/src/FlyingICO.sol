// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

/*
  Flying ICO â€” Simplified Deposit Contract
  - Users deposit accepted assets in form of accepted ERC20s / ETH
  - USD value is taken from Chainlink price feeds (aggregators).
  - Mint Tokens at X Tokens per $1 USD contributed.
  - Token maximum supply: X Tokens (with 18 decimals).
  - When minted on primary, Tokens are held by this contract and tracked in a PerpetualPUT Position.
  - Users can Redeem (burn Tokens from their position and get back original asset amount)
    or Claim (release Tokens to user, invalidating the PUT portion and freeing backing
    to the protocol pool).
  - Vesting & Post-Vesting Behavior:
    - All minted Tokens are subject to a linear vesting schedule.
    - Before vesting starts, 100% of tokens are redeemable (_BPS = 10000).
    - During vesting, the redeemable amount decreases linearly over time.
    - After the vesting period ends, the vesting rate becomes 0% and *all tokens remain locked*. Users cannot redeem through the vesting mechanism anymore.
    - Post-vesting lockup prevents redemptions
        using potentially stale fair prices. Users must wait for NAV-based
        redemptions or a future claim mechanism defined by the protocol.
*/

import {ERC20} from "@openzeppelin/token/ERC20/ERC20.sol";
import {ERC20Permit} from "@openzeppelin/token/ERC20/extensions/ERC20Permit.sol";
import {IERC20} from "@openzeppelin/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "@openzeppelin/token/ERC20/extensions/IERC20Metadata.sol";
import {SafeERC20} from "@openzeppelin/token/ERC20/utils/SafeERC20.sol";
import {Math} from "@openzeppelin/utils/math/Math.sol";
import {ReentrancyGuard} from "@openzeppelin/utils/ReentrancyGuard.sol";
import {AggregatorV3Interface} from "@chainlink/shared/interfaces/AggregatorV3Interface.sol";
import {ChainlinkLibrary} from "./utils/Chainlink.sol";

contract FlyingICO is ERC20, ERC20Permit, ReentrancyGuard {
    using Math for uint256;
    using SafeERC20 for IERC20;

    // ========================================================================
    // Constants ==============================================================
    // ========================================================================

    address private constant _ETH_ADDR = address(0);
    uint256 private constant _WAD = 1e18;
    uint256 private constant _BPS = 10_000; // 100%
    uint256 private constant _MIN_MINT_PER_POSITION = 1e18; // 1 token (in token units)
    uint256 private constant _MAX_FEED_DECIMALS = 18;
    uint256 private constant _MAX_ASSET_DECIMALS = 18;
    uint256 private constant _MAX_FREQUENCY = 30 days;

    uint256 private immutable _VESTING_START;
    uint256 private immutable _VESTING_END;
    uint256 private immutable _TOKENS_CAP;
    uint256 private immutable _TOKENS_PER_USD;
    address private immutable _TREASURY;
    address private immutable _SEQUENCER; // only for L2s

    // ========================================================================
    // Structs ===============================================================
    // ========================================================================

    struct Position {
        address user; // owner of the position
        address asset; // asset used at deposit (_ETH_ADDR for native)
        uint256 assetAmount; // amount of original asset reserved (in asset decimals)
        uint256 tokenAmount; // amount of Tokens (in units) reserved and locked in PUT
        uint256 vestingAmount; // amount of Tokens (in units) that is vesting and not redeemable yet
    }

    struct PriceFeed {
        AggregatorV3Interface feed;
        uint256 frequency;
    }

    // ========================================================================
    // Errors =================================================================
    // ========================================================================

    error FlyingICO__InvalidArraysLength(uint256 length1, uint256 length2, uint256 length3);
    error FlyingICO__ZeroValue();
    error FlyingICO__AssetNotAccepted(address asset);
    error FlyingICO__ZeroUsdValue();
    error FlyingICO__ZeroTokenAmount();
    error FlyingICO__TokensCapExceeded();
    error FlyingICO__ZeroPrice(address asset);
    error FlyingICO__InsufficientBacking();
    error FlyingICO__TransferFailed();
    error FlyingICO__InsufficientAssetAmount();
    error FlyingICO__InsufficientEther();
    error FlyingICO__NotEnoughLockedTokens();
    error FlyingICO__Unauthorized();
    error FlyingICO__ZeroAddress();
    error FlyingICO__InvalidVestingSchedule(uint256 currentTime, uint256 vestingStart, uint256 vestingEnd);
    error FlyingICO__NotEnoughRedeemableTokens(uint256 positionId, uint256 tokensToBurn, uint256 availableTokens);
    error FlyingICO__DepositTooSmall(uint256 tokenAmount, uint256 minTokenAmount);
    error FlyingICO__InvalidPriceFeed(address asset, address feed);
    error FlyingICO__InvalidFrequency(address asset, uint256 frequency);
    error FlyingICO__UnsupportedFeedDecimals(address asset, uint8 decimals);
    error FlyingICO__UnsupportedAssetDecimals(address asset, uint8 decimals);

    // ========================================================================
    // Events =================================================================
    // ========================================================================

    event FlyingICO__Initialized(
        string name,
        string symbol,
        uint256 tokenCap,
        uint256 tokensPerUsd,
        address[] acceptedAssets,
        address[] priceFeeds,
        uint256[] frequencies,
        address sequencer,
        address treasury,
        uint256 vestingStart,
        uint256 vestingEnd
    );
    event FlyingICO__Deposited(
        address indexed user, uint256 positionId, address asset, uint256 assetAmount, uint256 tokensMinted
    );
    event FlyingICO__Redeemed(
        address indexed user,
        uint256 positionId,
        uint256 tokensBurned,
        address assetReturned,
        uint256 assetReturnedAmount
    );
    event FlyingICO__Claimed(
        address indexed user,
        uint256 positionId,
        uint256 tokensClaimed,
        address assetReleased,
        uint256 assetReleasedAmount
    );
    event FlyingICO__PositionClosed(address indexed user, uint256 positionId);
    event FlyingICO__AssetsTakenToTreasury(
        address indexed asset,
        uint256 assetAmount
    );

    // ========================================================================
    // State Variables ========================================================
    // ========================================================================

    uint256 public nextPositionId;
    mapping(uint256 => Position) public positions; // positionId -> position
    mapping(address => uint256[]) private _positionsOf; // user -> positionsIds
    mapping(address => uint256) public backingBalances; // Backing assets held as backing for open PUTs (asset -> amount)
    mapping(address => PriceFeed) public priceFeeds; // asset -> price feed (USD)

    // ========================================================================
    // Constructor ============================================================
    // ========================================================================

    constructor(
        string memory name_,
        string memory symbol_,
        uint256 tokenCap_, // in tokens, not units
        uint256 tokensPerUsd_, // 10 Tokens per $1
        address[] memory acceptedAssets_,
        address[] memory priceFeeds_,
        uint256[] memory frequencies_,
        address sequencer_,
        address treasury_,
        uint256 vestingStart_,
        uint256 vestingEnd_
    ) ERC20(name_, symbol_) ERC20Permit(name_) {
        if (acceptedAssets_.length != priceFeeds_.length || acceptedAssets_.length != frequencies_.length) {
            revert FlyingICO__InvalidArraysLength(acceptedAssets_.length, priceFeeds_.length, frequencies_.length);
        }

        // set accepted assets and price feeds (+ validate config)
        for (uint256 i = 0; i < acceptedAssets_.length; i++) {
            if (priceFeeds_[i] == address(0)) {
                revert FlyingICO__InvalidPriceFeed(acceptedAssets_[i], priceFeeds_[i]);
            }

            if (frequencies_[i] == 0 || frequencies_[i] > _MAX_FREQUENCY) {
                revert FlyingICO__InvalidFrequency(acceptedAssets_[i], frequencies_[i]);
            }

            uint8 feedDecimals = AggregatorV3Interface(priceFeeds_[i]).decimals();
            if (feedDecimals > _MAX_FEED_DECIMALS) {
                revert FlyingICO__UnsupportedFeedDecimals(acceptedAssets_[i], feedDecimals);
            }

            // For ERC20 assets, validate asset decimals to avoid overflow in 10**decimals math.
            if (acceptedAssets_[i] != _ETH_ADDR) {
                uint8 assetDecimals = IERC20Metadata(acceptedAssets_[i]).decimals();
                if (assetDecimals > _MAX_ASSET_DECIMALS) {
                    revert FlyingICO__UnsupportedAssetDecimals(acceptedAssets_[i], assetDecimals);
                }
            }

            priceFeeds[acceptedAssets_[i]] =
                PriceFeed({feed: AggregatorV3Interface(priceFeeds_[i]), frequency: frequencies_[i]});
        }

        if (treasury_ == address(0)) {
            revert FlyingICO__ZeroAddress();
        }

        if (vestingStart_ < block.timestamp || vestingEnd_ < vestingStart_) {
            revert FlyingICO__InvalidVestingSchedule(block.timestamp, vestingStart_, vestingEnd_);
        }

        _TOKENS_CAP = tokenCap_ * _WAD;
        _TOKENS_PER_USD = tokensPerUsd_ * _WAD;
        _TREASURY = treasury_;
        _SEQUENCER = sequencer_;
        _VESTING_START = vestingStart_;
        _VESTING_END = vestingEnd_;

        emit FlyingICO__Initialized(
            name_,
            symbol_,
            tokenCap_,
            tokensPerUsd_,
            acceptedAssets_,
            priceFeeds_,
            frequencies_,
            sequencer_,
            treasury_,
            vestingStart_,
            vestingEnd_
        );
    }

    // ========================================================================
    // External Functions =====================================================
    // ========================================================================

    /// @notice Deposit ETH into the contract
    /// @return positionId the id of the position created
    function depositEther() external payable nonReentrant returns (uint256 positionId) {
        positionId = _deposit(_ETH_ADDR, msg.value);
    }

    /// @notice Deposit an accepted ERC20 token. Caller must have approved this contract for `assetAmount`.
    /// @param asset the asset to deposit
    /// @param assetAmount the amount of asset to deposit
    /// @return positionId the id of the position created
    function depositERC20(address asset, uint256 assetAmount) external nonReentrant returns (uint256 positionId) {
        positionId = _deposit(asset, assetAmount);
        IERC20(asset).safeTransferFrom(msg.sender, address(this), assetAmount);
    }

    /// @notice Redeem some or all of your Perpetual PUT (burn locked Tokens and receive the original asset back at par)
    /// @param positionId Id of the position created at deposit
    /// @param tokensToBurn amount of Tokens (in Tokens units) to redeem from that position
    function redeem(uint256 positionId, uint256 tokensToBurn) external nonReentrant returns (uint256 assetAmount) {
        uint256 availableTokens = redeemableTokens(positionId);

        if (tokensToBurn > availableTokens) {
            revert FlyingICO__NotEnoughRedeemableTokens(positionId, tokensToBurn, availableTokens);
        }

        assetAmount = _exitPosition(positionId, tokensToBurn);

        _burn(address(this), tokensToBurn);

        // Transfer asset back to user
        if (positions[positionId].asset == _ETH_ADDR) {
            // native ETH
            (bool sent,) = msg.sender.call{value: assetAmount}("");

            if (!sent) {
                revert FlyingICO__TransferFailed();
            }
        } else {
            IERC20(positions[positionId].asset).safeTransfer(msg.sender, assetAmount);
        }

        emit FlyingICO__Redeemed(msg.sender, positionId, tokensToBurn, positions[positionId].asset, assetAmount);
    }

    /// @notice Claim Tokens from your Perpetual PUT. This invalidates the PUT on that portion forever and transfers Tokens to the user.
    /// The backing previously reserved becomes available for protocol operations.
    /// @param positionId Id of the position created at deposit
    /// @param tokensToClaim amount of Tokens (in Tokens units) to claim from that position
    function claim(uint256 positionId, uint256 tokensToClaim) external nonReentrant returns (uint256 assetAmount) {
        assetAmount = _exitPosition(positionId, tokensToClaim);

        // Transfer Tokens from contract to user (these Tokens lose the Perpetual PUT)
        // The Tokens are already minted and sitting in this contract
        _transfer(address(this), msg.sender, tokensToClaim);

        // Released backing becomes available for protocol operations:
        // Backing has been reduced above, so the released assets are now available
        // to the treasury for protocol operations via takeAssetsToTreasury.
        emit FlyingICO__Claimed(msg.sender, positionId, tokensToClaim, positions[positionId].asset, assetAmount);
    }

    /// @notice Take assets from the contract to the treasury
    /// @param asset asset to take
    /// @param assetAmount amount of asset to take
    function takeAssetsToTreasury(address asset, uint256 assetAmount) external nonReentrant {
        if (msg.sender != _TREASURY) {
            revert FlyingICO__Unauthorized();
        }

        if (assetAmount == 0) {
            revert FlyingICO__ZeroValue();
        }

        if (!_acceptedAsset(asset)) {
            revert FlyingICO__AssetNotAccepted(asset);
        }

        if (asset == _ETH_ADDR) {
            uint256 availableEther = address(this).balance - backingBalances[asset];

            if (availableEther < assetAmount) {
                revert FlyingICO__InsufficientEther();
            }

            (bool sent,) = payable(_TREASURY).call{value: assetAmount}("");
            if (!sent) {
                revert FlyingICO__TransferFailed();
            }
        } else {
            uint256 availableAssets = IERC20(asset).balanceOf(address(this)) - backingBalances[asset];

            if (availableAssets < assetAmount) {
                revert FlyingICO__InsufficientAssetAmount();
            }

            IERC20(asset).safeTransfer(_TREASURY, assetAmount);
        }

        emit FlyingICO__AssetsTakenToTreasury(asset, assetAmount);
    }

    /// @notice Get the positions of a user
    /// @param user the user to get the positions of
    /// @return positions the positions of the user
    function positionsOf(address user) external view returns (uint256[] memory) {
        return _positionsOf[user];
    }

    // ========================================================================
    // Internal functions =====================================================
    // ========================================================================

    /// @notice Internal function to deposit an asset
    /// @param asset the asset to deposit
    /// @param assetAmount the amount of asset to deposit
    /// @return positionId the id of the position created
    function _deposit(address asset, uint256 assetAmount) internal returns (uint256 positionId) {
        if (assetAmount == 0) {
            revert FlyingICO__ZeroValue();
        }

        if (!_acceptedAsset(asset)) {
            revert FlyingICO__AssetNotAccepted(asset);
        }

        // compute token amount
        uint256 tokenAmount = _computeTokenAmount(asset, assetAmount);
        // mint tokens to this contract
        _mint(address(this), tokenAmount);
        // record backing
        backingBalances[asset] += assetAmount;
        // create position
        positionId = nextPositionId++;
        positions[positionId] = Position({
            user: msg.sender,
            asset: asset,
            assetAmount: assetAmount,
            tokenAmount: tokenAmount,
            vestingAmount: tokenAmount
        });
        _positionsOf[msg.sender].push(positionId);

        emit FlyingICO__Deposited(msg.sender, positionId, asset, assetAmount, tokenAmount);
    }

    function _exitPosition(uint256 positionId, uint256 tokenAmount) internal returns (uint256 assetAmount) {
        if (tokenAmount == 0) {
            revert FlyingICO__ZeroValue();
        }

        if (positions[positionId].user != msg.sender) {
            revert FlyingICO__Unauthorized();
        }

        if (positions[positionId].tokenAmount < tokenAmount) {
            revert FlyingICO__NotEnoughLockedTokens();
        }

        // compute proportional asset return
        assetAmount = _computeAssetAmount(positionId, tokenAmount);

        // Update position
        Position storage position = positions[positionId];
        position.tokenAmount -= tokenAmount;
        position.assetAmount -= assetAmount;

        // if vesting is not started, reduce the vesting amount
        if (block.timestamp < _VESTING_START) {
            position.vestingAmount -= tokenAmount;
        }

        // reduce backing
        backingBalances[position.asset] -= assetAmount;

        // Emit an explicit lifecycle signal for indexers / UIs.
        if (position.tokenAmount == 0) {
            emit FlyingICO__PositionClosed(msg.sender, positionId);
        }
    }

    /// @notice Internal function to compute the token amount for a deposit
    /// @param asset the asset to deposit
    /// @param assetAmount the amount of asset to deposit
    /// @return tokenAmount the amount of tokens to mint
    function _computeTokenAmount(address asset, uint256 assetAmount) internal view returns (uint256 tokenAmount) {
        // compute USD value
        uint256 usdValue = _assetToUsdValue(asset, assetAmount);

        if (usdValue == 0) {
            revert FlyingICO__ZeroUsdValue();
        }

        // Tokens to mint
        tokenAmount = usdValue.mulDiv(_TOKENS_PER_USD, _WAD, Math.Rounding.Floor);

        if (tokenAmount == 0) {
            revert FlyingICO__ZeroTokenAmount();
        }

        if (tokenAmount < _MIN_MINT_PER_POSITION) {
            revert FlyingICO__DepositTooSmall(tokenAmount, _MIN_MINT_PER_POSITION);
        }

        // Enforce cap
        if (totalSupply() + tokenAmount > _TOKENS_CAP) {
            revert FlyingICO__TokensCapExceeded();
        }
    }

    /// @notice Internal function to compute the asset amount for a redeem
    /// @param positionId Id of the position created at deposit
    /// @param tokenAmount amount of Tokens (in Tokens units) to redeem from that position
    /// @return assetAmount the amount of asset to return
    function _computeAssetAmount(uint256 positionId, uint256 tokenAmount) internal view returns (uint256 assetAmount) {
        Position memory position = positions[positionId];

        if (position.tokenAmount == 0) {
            revert FlyingICO__ZeroTokenAmount();
        }

        assetAmount = tokenAmount.mulDiv(position.assetAmount, position.tokenAmount, Math.Rounding.Floor);

        if (assetAmount == 0) {
            revert FlyingICO__ZeroValue();
        }

        if (position.assetAmount < assetAmount) {
            // invariant - this should never happen
            revert FlyingICO__InsufficientAssetAmount();
        }

        if (backingBalances[position.asset] < assetAmount) {
            // invariant - this should never happen
            revert FlyingICO__InsufficientBacking();
        }
    }

    // Convert an asset amount (raw asset units) into USD with 18 decimals precision
    /// @param asset the asset to convert
    /// @param assetAmount the amount of asset to convert
    /// @return usdValue the USD value of the asset in USD with 18 decimals precision
    function _assetToUsdValue(address asset, uint256 assetAmount) internal view returns (uint256 usdValue) {
        PriceFeed memory priceFeed = priceFeeds[asset];

        uint256 price = ChainlinkLibrary.getPrice(address(priceFeed.feed), priceFeed.frequency, _SEQUENCER);
        uint256 feedUnits = 10 ** uint256(priceFeed.feed.decimals());
        uint256 assetUnits = 10 ** _getDecimals(asset);

        usdValue = assetAmount.mulDiv(price * _WAD, assetUnits * feedUnits, Math.Rounding.Floor);
    }

    // Read ERC20 decimals
    /// @param token the token to get the decimals of
    /// @return decimals the decimals of the token
    function _getDecimals(address token) internal view returns (uint256) {
        if (token == _ETH_ADDR) return 18;
        uint8 decimals = IERC20Metadata(token).decimals();
        if (decimals > _MAX_ASSET_DECIMALS) {
            revert FlyingICO__UnsupportedAssetDecimals(token, decimals);
        }

        return uint256(decimals);
    }

    /// @notice Internal function to check if an asset is accepted
    /// @param asset the asset to check
    /// @return true if the asset is accepted, false otherwise
    function _acceptedAsset(address asset) internal view returns (bool) {
        return address(priceFeeds[asset].feed) != address(0);
    }

    /* ========================================================================
    * =========================== Vesting Schedule ============================
    * =========================================================================
    */

    /**
     * @dev Returns the redeemable tokens of the position based on the current vesting schedule.
     *
     * IMPORTANT: This function returns 0 after the vesting period ends, effectively
     * locking all shares until NAV redemptions are enabled by the owner.
     *
     * Vesting phases:
     * - Before vesting starts: Returns 100% of user's vesting shares
     * - During vesting: Returns linearly decreasing amount based on time remaining
     * - After vesting ends: Returns 0 (shares are locked until NAV mode is enabled)
     *
     * @param positionId The id of the position to query the redeemable tokens for
     * @return The redeemable tokens (0 if vesting period has ended)
     */
    function redeemableTokens(uint256 positionId) public view returns (uint256) {
        uint256 redeemable = vestingRate().mulDiv(positions[positionId].vestingAmount, _BPS, Math.Rounding.Floor);
        uint256 takenTokens = positions[positionId].vestingAmount - positions[positionId].tokenAmount;

        return redeemable > takenTokens ? (redeemable - takenTokens) : 0;
    }

    /**
     * @dev Returns the current vesting rate of the vault.
     *
     * The vesting rate determines what percentage of vested shares are currently redeemable:
     * - Before vesting starts: 10000 (100% - all shares redeemable)
     * - During vesting: Decreases linearly from 10000 to 0
     * - After vesting ends: 0 (0% - no shares redeemable via vesting)
     *
     * @return The vesting rate as a percentage in _BPS (10000 = 100%, 0 = 0%)
     */
    function vestingRate() public view returns (uint256) {
        return _calculateVestingRate();
    }

    /**
     * @dev Calculates the current vesting rate based on the vesting schedule.
     *
     * This function implements the core vesting logic:
     * 1. Pre-vesting: Returns 100% (_BPS = 10000)
     * 2. During vesting: Returns linearly decreasing rate
     * 3. Post-vesting: Returns 0% - THIS LOCKS ALL SHARES
     *
     * The post-vesting behavior (returning 0) is intentional and prevents
     * redemptions at potentially stale fair prices after the vesting period.
     * Users must wait for NAV redemptions to be enabled to access their funds.
     *
     * @return The vesting rate in basis points (0-10000)
     */
    function _calculateVestingRate() internal view returns (uint256) {
        if (block.timestamp < _VESTING_START) {
            return _BPS;
        }

        if (block.timestamp > _VESTING_END) {
            return 0;
        }

        //                vesting end - current time
        // vesting rate = ---------------------------- x _BPS
        //                vesting end - vesting start

        return _BPS.mulDiv(_VESTING_END - block.timestamp, _VESTING_END - _VESTING_START, Math.Rounding.Floor);
    }
}
